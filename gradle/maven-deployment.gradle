//see https://docs.gradle.org/current/userguide/maven_plugin.html
apply plugin: 'maven'
apply plugin: 'maven-publish'

ext.gh = [
	dep: 'https://github.com',
	org: 'opencaesar',
	rep: 'ecore-tools'
]

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "melaasar"
            name "Maged Elaasar"
            email "melaasar@gmail.com"
        }
    }

    scm {
        url gh.dep+'/'+gh.org+'/'+gh.rep
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        	afterEvaluate {
				plugins.withType(ApplicationPlugin) {
					artifact distZip
					artifact distTar
			    }
        	}
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.description)
                root.appendNode('name', project.ext.title)
                root.appendNode('url', gh.dep+'/'+gh.org+'/'+gh.rep)
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/opencaesar/ecore-tools"
            credentials {
                username = System.getenv("GITHUB_USER")
                password = System.getenv("GITHUB_API_KEY")
            }
        }
    }
}